plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:4.5.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    querydsl 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    querydsl 'jakarta.persistence:jakarta.persistence-api:3.1.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    runtimeOnly 'com.mysql:mysql-connector-j'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'

    implementation 'org.springframework.boot:spring-boot-starter-websocket'
}

tasks.named('test') {
    useJUnitPlatform()
}

def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main {
        java {
            srcDirs += querydslDir
        }
    }
}

task compileQuerydsl(type: JavaCompile) {
    group = 'build'
    description = 'Generate QueryDSL Q-classes'

    source = sourceSets.main.java
    classpath = sourceSets.main.compileClasspath + configurations.querydsl
    destinationDirectory = file(querydslDir)
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)

    options.compilerArgs += [
        '-proc:only',
        '-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
    ]
    options.annotationProcessorPath = configurations.querydsl
}

tasks.named('compileJava') {
    dependsOn compileQuerydsl
}